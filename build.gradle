apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'ivy-publish'
apply plugin: 'publishing'

sourceCompatibility = 1.7
version = "1.0.0"
group = "com.github.truemped.heritrix"
archivesBaseName = "heritrix-rest-client"
jar.baseName = archivesBaseName

task sourceJar(type: Jar) {
	from sourceSets.main.java
	classifier "sources"
}

artifacts {
	archives(sourceJar){
		type "sources"
	}
}

// for publishing to an ivy repository
publishing {
	publications {
		ivyJava (IvyPublication) {
			organisation 'com.github.truemped.heritrix'
			module 'heritrix-rest-client'
			revision '1.0.0'
						
			from components.java
			
			artifact (sourceJar) {
				type 'sources'
				conf 'dev'
			}
			configurations {
				dev {
					extend 'runtime'
				}
			}
		}
		
	}
	repositories {
		ivy {
			// change to point to your repo, e.g. http://my.org/repo
			url "${rootProject.buildDir}/repo"
		}
	}
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Heritrix Rest Client', 'Implementation-Version': version
    }
}

repositories {
	mavenCentral()
}

dependencies {
	compile 'org.apache.httpcomponents:httpclient:4.0.1'
	compile 'org.apache.httpcomponents:httpcore:4.0.1'
	compile 'commons-lang:commons-lang:2.6'
	compile 'org.slf4j:slf4j-api:1.7.9'
	testCompile 'org.slf4j:slf4j-simple:1.7.9'

	testCompile group: 'junit', name: 'junit', version: '4.+'
}

model {
	tasks.generateDescriptorFileForIvyJavaPublication {
		destination = file("$buildDir/generated-ivy.xml")
	}
}